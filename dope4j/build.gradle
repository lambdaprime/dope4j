// include xfunction for updateVersion
buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "io.github.lambdaprime:id.xfunction:14.0"
  }
}

plugins {
  id 'com.diffplug.spotless'
  id 'java-library'
}

apply plugin : 'eclipse'

def releaseFileName = "${projectName}-v${version}.zip"
  
dependencies {
  api "io.github.lambdaprime:id.xfunction:20.0"
  api "io.github.lambdaprime:matcv:3.0"
  api "org.openpnp:opencv:4.6.0-0"

  def djlVersion = "0.20.0"
  api "ai.djl.pytorch:pytorch-jni:1.13.0-${djlVersion}"
  api "ai.djl:api:${djlVersion}"
  api "ai.djl.pytorch:pytorch-engine:${djlVersion}"
  api "ai.djl.opencv:opencv:${djlVersion}"
  api("ai.djl.onnxruntime:onnxruntime-engine:${djlVersion}") {
    exclude group: "com.microsoft.onnxruntime", module: "onnxruntime"
  }
  api "com.microsoft.onnxruntime:onnxruntime_gpu:1.12.1"
  api "ai.djl.pytorch:pytorch-native-cu117:1.13.0:linux-x86_64"
  api "org.slf4j:slf4j-api:1.7.36"
  api "io.opentelemetry:opentelemetry-api:1.22.0"
}

jar {
  manifest { 
    attributes 'Implementation-Version': "${rootProject.version}"
  }
}

javadoc {
  options.noTimestamp = true
  options.noQualifiers = ["all"]
  options.noTree = true
  options.noIndex = true
  options.noNavBar = true
  options.overview = "src/overview.html"
  // ignore missing @return, @param etc
  options.addBooleanOption('Xdoclint:all,-missing', true)
  exclude 'id/dope4j/impl/**'
}

task createRelease(type: Copy) {
  def out = 'release'
  from jar.archiveFile
  into out
}

task cleanRelease(type: Delete) {
  delete "$projectDir/release/*-SNAPSHOT.zip"
}

assemble.dependsOn createRelease
clean.dependsOn cleanRelease

task copyReadme(type: Copy) {
  from("../README.md")
  into("$buildDir/resources/main")
  rename ("README.md", "README-${projectName}.md")
}

// overwrite template README which comes from resources folder
jar.dependsOn copyReadme
javadoc.dependsOn copyReadme

// run spotless and format code before the build
classes.dependsOn spotlessApply

task updateVersion() {
  // update version only when it is specified and non SNAPSHOT version is built
  // 'unspecified' is the default value for the version in Gradle if it is not set
  if (!version || version == "unspecified" || version.contains("SNAPSHOT")) return;
  println "Updating version to " + version
  new id.xfunction.text.Substitutor()
    .withRegexpSupport()
    .substitute(java.nio.file.Paths.get("${project.rootDir}/README.md"), Map.of(
      "io.github.lambdaprime:dope4j:[0-9.]+", "io.github.lambdaprime:dope4j:" + version
    ));
}
createRelease.dependsOn updateVersion

if (project.hasProperty('publish')) {

  // Initially it was taken from https://central.sonatype.org/publish/publish-gradle/
  // but since Gradle removed this plugin https://docs.gradle.org/7.0/userguide/upgrading_version_6.html#removal_of_the_legacy_maven_plugin
  // it was rewritten for new one

  task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from "README-javadoc.txt"
  }

  task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from "README-sources.txt"
  }

  java {
    withJavadocJar()
    withSourcesJar()
  }

  apply plugin: 'maven-publish'

  publishing {
    publications {
      mavenJava(MavenPublication) {
        artifactId = 'dope4j'
        groupId = 'io.github.lambdaprime'
        from components.java
        pom {
          name = 'dope4j'
          description = 'Deep Object Pose Estimation for Java'
          url = 'https://github.com/lambdaprime/dope4j'

          licenses {
            license {
              name = 'The Apache License, Version 2.0'
              url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
          }

          developers {
            developer {
              id = 'lambdaprime'
              email = 'intid@protonmail.com'
            }
          }

          scm {
            url = 'https://github.com/lambdaprime/dope4j'
          }
        }
      }
    }
    repositories {
      maven {
        credentials {
          username ossrhUsername
          password ossrhPassword
        }
        url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      }
    }    
  }

  if (project.hasProperty('signing.secretKeyRingFile')) {
    apply plugin: 'signing'

    signing {
      sign publishing.publications.mavenJava
    }
  }
}
